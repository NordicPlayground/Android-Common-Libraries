plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}
apply from: rootProject.file("gradle/git-tag-version.gradle")

group = GROUP

android {
    namespace 'no.nordicsemi.android.common.ui.logger'
    compileSdk android_api_version

    defaultConfig {
        minSdk android_min_api_version
        targetSdk android_api_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles "navigation-proguard-rules.pro"
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }

    buildFeatures {
        compose true
    }
}
hilt {
    enableAggregatingTask = true
}
kapt {
    correctErrorTypes true
}

dependencies {
    implementation project(":theme")
    implementation "no.nordicsemi.android:log:2.3.0"

    // Fundamental components of compose UI
    implementation "androidx.compose.ui:ui:$compose_version"
    // Previews
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation:$compose_version"
    // Material 3
    implementation "androidx.compose.material3:material3:$material3_version"

    implementation "com.google.dagger:hilt-android:$dagger_version"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_version"
}

// === Maven Central configuration ===
// The following file exists only when Android BLE Library project is opened, but not
// when the module is loaded to a different project.
if (rootProject.file('gradle/publish-module.gradle').exists()) {
    ext {
        POM_ARTIFACT_ID = 'uilogger'
        POM_NAME = 'Nordic library for library for UI helpers utilizing nordic logger library.'
        POM_PACKAGING = 'aar'
    }
    apply from: rootProject.file('gradle/publish-module.gradle')
}

