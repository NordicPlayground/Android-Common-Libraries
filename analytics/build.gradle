apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.protobuf'
apply from: rootProject.file("gradle/git-tag-version.gradle")

group = GROUP

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.14.0'
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

android {
    namespace 'no.nordicsemi.android.common.analytics'
    compileSdk android_api_version

    defaultConfig {
        minSdk android_min_api_version
        targetSdk android_api_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles "analytics-rules.pro"
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }

    buildFeatures {
        compose true
    }

    hilt {
        enableExperimentalClasspathAggregation = true
    }
}

dependencies {
    implementation project(":core")
    implementation project(":theme")

    // Fundamental components of compose UI
    implementation "androidx.compose.ui:ui:$compose_version"
    // Previews
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation:$compose_version"
    // Material 3
    implementation "androidx.compose.material3:material3:$material3_version"
    // Data Store
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation "com.google.dagger:hilt-android:$dagger_version"
    kapt "com.google.dagger:hilt-compiler:$dagger_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    implementation platform('com.google.firebase:firebase-bom:30.5.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
}

// === Maven Central configuration ===
// The following file exists only when Android BLE Library project is opened, but not
// when the module is loaded to a different project.
if (rootProject.file('gradle/publish-module.gradle').exists()) {
    ext {
        POM_ARTIFACT_ID = 'analytics'
        POM_NAME = 'Nordic library for analytics.'
        POM_PACKAGING = 'aar'
    }
    apply from: rootProject.file('gradle/publish-module.gradle')
}
