plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}
apply from: rootProject.file("gradle/git-tag-version.gradle")

group = GROUP

android {
    namespace 'no.nordicsemi.android.common.navigation'
    compileSdk android_api_version

    defaultConfig {
        minSdk android_min_api_version
        targetSdk android_api_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles "navigation-proguard-rules.pro"
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }

    buildFeatures {
        compose true
    }
}
hilt {
    enableAggregatingTask = true
}
kapt {
    correctErrorTypes true
}

dependencies {
    implementation platform("androidx.compose:compose-bom:$compose_version")
    // Fundamental components of compose UI
    implementation "androidx.compose.ui:ui"
    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    // Integration with activities
    implementation "androidx.activity:activity-compose:1.6.1"
    // Navigation
    implementation "androidx.navigation:navigation-compose:2.6.0-alpha03"

    // Dagger and Hilt
    implementation "com.google.dagger:hilt-android:$dagger_version"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
}

// === Maven Central configuration ===
// The following file exists only when Android BLE Library project is opened, but not
// when the module is loaded to a different project.
if (rootProject.file('gradle/publish-module.gradle').exists()) {
    ext {
        POM_ARTIFACT_ID = 'navigation'
        POM_NAME = 'Nordic library for navigation framework.'
        POM_PACKAGING = 'aar'
    }
    apply from: rootProject.file('gradle/publish-module.gradle')
}
