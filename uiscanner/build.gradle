apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply from: rootProject.file("gradle/git-tag-version.gradle")

group = GROUP

android {
    namespace 'no.nordicsemi.android.common.ui.scanner'
    compileSdk android_api_version

    defaultConfig {
        minSdk android_min_api_version
        targetSdk android_api_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles "uiscanner-proguard-rules.pro"
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }

    buildFeatures {
        compose true
    }

    hilt {
        enableExperimentalClasspathAggregation = true
    }
}

dependencies {
    implementation project(":core")
    implementation project(":theme")
    implementation project(":permission")

    implementation "androidx.compose.material3:material3:1.0.0-beta01"
    implementation "com.google.accompanist:accompanist-flowlayout:0.24.3-alpha"

    implementation "androidx.compose.ui:ui:$compose_version"
    // Required for Compose Preview
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation:$compose_version"
    // Material design icons
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    // Integration with activities
    implementation "androidx.activity:activity-compose:1.5.1"

    // Brings the new BluetoothLeScanner API to older platforms
    api "no.nordicsemi.android.support.v18:scanner:1.6.0"

    api "com.google.dagger:hilt-android:2.43.1"
    kapt "com.google.dagger:hilt-compiler:2.43.1"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
}

// === Maven Central configuration ===
// The following file exists only when Android BLE Library project is opened, but not
// when the module is loaded to a different project.
if (rootProject.file('gradle/publish-module.gradle').exists()) {
    ext {
        POM_ARTIFACT_ID = 'uiscanner'
        POM_NAME = 'Nordic library for Android with UI screens utilizing uiscanner library.'
        POM_PACKAGING = 'aar'
    }
    apply from: rootProject.file('gradle/publish-module.gradle')
}
