plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}
apply from: rootProject.file("gradle/git-tag-version.gradle")

group = GROUP

android {
    namespace 'no.nordicsemi.android.common.ui.scanner'
    compileSdk android_api_version

    defaultConfig {
        minSdk android_min_api_version
        targetSdk android_api_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles "uiscanner-proguard-rules.pro"
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }

    buildFeatures {
        compose true
    }
}
hilt {
    enableAggregatingTask = true
}
kapt {
    correctErrorTypes true
}

dependencies {
    implementation project(":core")
    implementation project(":theme")
    implementation project(":permission")

    implementation "com.google.accompanist:accompanist-flowlayout:0.27.0"
    implementation "com.google.accompanist:accompanist-swiperefresh:0.27.0"

    implementation platform("androidx.compose:compose-bom:$compose_version")
    // Material 3
    implementation "androidx.compose.material3:material3"
    // Material design icons
    implementation "androidx.compose.material:material-icons-extended"
    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    // Integration with activities
    implementation "androidx.activity:activity-compose:1.6.1"
    // Brings the new BluetoothLeScanner API to older platforms
    implementation "no.nordicsemi.android.support.v18:scanner:1.6.0"

    implementation "com.google.dagger:hilt-android:$dagger_version"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
}

// === Maven Central configuration ===
// The following file exists only when Android BLE Library project is opened, but not
// when the module is loaded to a different project.
if (rootProject.file('gradle/publish-module.gradle').exists()) {
    ext {
        POM_ARTIFACT_ID = 'uiscanner'
        POM_NAME = 'Nordic library for Android with UI screens utilizing uiscanner library.'
        POM_PACKAGING = 'aar'
    }
    apply from: rootProject.file('gradle/publish-module.gradle')
}
